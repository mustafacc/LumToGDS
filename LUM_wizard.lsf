#How to use:
#1. Press F5 or Run
#The script will first find all geometry objects and store them to a 'metadata' variable (LIB_GDS_extract)
#The script will then initiate a wizard to allow the user to input layer information corresponding to the detected material + geometry heights
#The Wizard will then prompt a filename and save to it.

#Exported GDS only readable in Klayout 0.26 or earlier
#Please use the python3.9 method or converter script if 
#you wish to use it for later Klayout versions.

#DEFAULT SETTINGS for Lumerical's library
n_circle = 64;	# number of sides to use for circle approximation (64 by default).
n_ring = 64;	# number of slices to use for ring approximation (64 by default).
n_custom = 64;	# number of slices to use for custom approximation (64 by default).
n_wg = 64;		# number of slices to use for waveguide approximation (64 by default). 
round_to_nm = 1;	# round the z and z span to the nearest integer of nm
grid = 1e-9;	# Round XY coordinates to this grid in SI. Will also update the database units if grid < 
max_objects = 10000;	# the maximum number of objects within the workspace (Increasing this will increase export time)
Lumerical_GDS_autoexp_functions; #loads the necessary functions
LUM_auto_detect;

#FUNCTIONS
function draw_entry_page(i,metadata){
    if(i==1){newwizardpage("Next");}
    else{newwizardpage("Next","Back");}
    
    wizardwidget("label","Entry "+num2str(i)+endl);
    wizardwidget("label","Material:\t\t"+metadata.material{i});
    wizardwidget("label","Dimension, z max:\t"+num2str(metadata.zmax{i}*1e6)+" [um]");
    wizardwidget("label","Dimension, z min:\t"+num2str(metadata.zmin{i}*1e6)+" [um]");
    wizardwidget("label",endl);    
    wizardwidget("checkbox","Include Layer",1);
    wizardwidget("number","Layer Number:",i);
    wizardwidget("number","DataType Number:",0); 
                              
}


metadata = create_export_metadata(get_objects_info());
layer_def = matrix(length(metadata.material),4); #exported to the gds generator script
#Create Wizard
newwizard(300,200,"Stack Definition Wizard");
wizardoption("fontsize",16);
wizardoption("fieldwidth",150);
wizardoption("fieldheight",20);
wizardoption("margin",20);
newwizardpage("Continue");

wizardwidget("label",endl+"The following layer information was auto-detected:"+endl);
wizardwidget("label","Number of Layers: "+num2str(length(metadata.material))); #list number of layers
wizardwidget("label","Layer Materials:"+endl); #list all materials used in the file
for (i=1;i<=length(metadata.material);i=i+1){
   wizardwidget("label",metadata.material{i}); 
}
out = runwizard; #0 = cancel, 1 is button 1, -1 is button 2
if(out==0){
   killwizard;
}

#Draw number of pages based on how many entries
#if back is pressed, load previous page via value of i
for(i=1;i<=length(metadata.material);0){
   draw_entry_page(i,metadata);
   out = runwizard; #0 = cancel, 1 is button 1, -1 is button 2
   if(out==0){
   killwizard;
   }

   else if((out==1)){
	  layer_def(i,1) = wizardgetdata(2);
	  layer_def(i,2) = wizardgetdata(3);
	  layer_def(i,3) = metadata.zmin{i};        
	  layer_def(i,4) = metadata.zmax{i};
	  i=i+1;         
	  #if(i<=length(metadata.material)){i=i+1;}
	  #return layer_def;
   }

   else if((out==-1)){
	  i=i-1;
	  if(i!=0){
		 #layer_def=draw_entry_page2(i,metadata,layer_def);
		 i=i-1;
		 }
	  #return layer_def;
   }	
}

#final confirmation page
newwizardpage("Export to GDS");
wizardwidget("label","Export Configuration (Leave blank for defaults)");
wizardwidget("string","GDS Filename\t(\"output.gds\"):","output.gds");
wizardwidget("string","Top Cell\t(\"model\"):",",model");
wizardwidget("label","Layers Export Information");
wizardwidget("label","Ly\t Dat\t Zmin\t Zmax");
for (i=1;i<=length(layer_def(:,1));i=i+1){
   wizardwidget("label",num2str(layer_def(i,1))+"\t"+num2str(layer_def(i,2))+"\t"+num2str(layer_def(i,3)*1e6)+"\t"+num2str(layer_def(i,4)*1e6)); 
}

out = runwizard;
if(out==0){
   killwizard;
}
else if(out==1){
   if (wizardgetdata(1)!=""){gds_filename = wizardgetdata(1);}
   else{gds_filename = "output.gds";}
   if (wizardgetdata(2)!=""){top_cell = wizardgetdata(2);}
   else{top_cell = "model";}
        killwizard;     
        Lumerical_GDS_autoexp; #this executes the gds export
        print("GDS Export Complete.");
}

clearfunctions;
clear(gds_filename);
clear(grid);
clear(layer_no);
clear(max_objects);
clear(n_circle);
clear(n_custom);
clear(n_ring);
clear(n_wg);
clear(orig_layer_def);
clear(out);
clear(round_to_nm);
clear(top_cell);
clear(layer_def);




