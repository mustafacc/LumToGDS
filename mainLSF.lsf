#How to use:
#1. Press F5 or Run
#The script will first find all geometry objects and store them to a 'metadata' variable (LIB_GDS_extract)
#The script will then initiate a wizard to allow the user to input layer information corresponding to the detected material + geometry heights
#The Wizard will then prompt a filename and save to it.

#Exported GDS only readable in Klayout 0.26 or earlier
#Please use the python3.9 method or converter script if 
#you wish to use it for later Klayout versions.

#Script has be in the library folder to properly call other library files.
thispath=splitstring(currentscriptname,"mainLSF.lsf");
cd(thispath{1});

GDSexport_core;
LUM_auto_detect;

##copy current file for exporting
#orifilename = currentfilename;
#copyfilename = "lumtogdscopytmp.fsp";
#save(copyfilename);
#load(copyfilename);
#copyfilename = currentfilename;
orifilename = currentfilename;
copyfilename = create_copy_file();


#FUNCTIONS
function draw_entry_page(i,metadata,dupdata){
    if(i==1){newwizardpage("Next");}
    else{newwizardpage("Next","Back");}
    
    wizardwidget("label","Entry "+num2str(i)+endl);
    if(metadata.material{i} == "<Object defined dielectric>"){
        wizardwidget("label","Material:\tIndex = "+metadata.index{i});
    }
    else{
        wizardwidget("label","Material:\t"+metadata.material{i});
    }
    wizardwidget("label","Z max:\t\t"+num2str(metadata.zmax{i}*1e6)+"\t[um]");
    wizardwidget("label","Z min:\t\t"+num2str(metadata.zmin{i}*1e6)+"\t[um]");
    wizardwidget("label","Z pos:\t\t"+num2str((metadata.zmax{i}+metadata.zmin{i})/2*1e6)+"\t[um]");    
    wizardwidget("label",endl);
    if(any(i==dupdata(:,2))){wizardwidget("label","<This object has a shared layer>");}
    else{    
    wizardwidget("checkbox","Include Layer",1);
    wizardwidget("number","Layer Number:",i);
    wizardwidget("number","DataType Number:",0); 
    }                          
}

metadata = get_objects_info();
dupdata = get_duplicate_layers(metadata);
dupe = dupdata(:,2);
ori = dupdata(:,1);
layer_def_list = cell(length(metadata.material));

#Create Wizard
newwizard(500,200,"Stack Definition Wizard");
wizardoption("fontsize",16);
wizardoption("fieldwidth",150);
wizardoption("fieldheight",20);
wizardoption("margin",20);
newwizardpage("Continue");

wizardwidget("label",endl+"The following layer information was auto-detected:"+endl);
wizardwidget("label","Unique Objects: "+num2str(length(metadata.material))); #list number of layers
wizardwidget("label","Materials Detected:"+endl); #list all materials used in the file
for (i=1;i<=length(metadata.material);i=i+1){
   if(!any(i==dupdata(:,2))){
       if (metadata.material{i} == "<Object defined dielectric>"){
           wizardwidget("label",">\tIndex = "+metadata.index{i});  
       }
       else{
           wizardwidget("label",">\t"+metadata.material{i});
       }
       }
}
out = runwizard; #0 = cancel, 1 is button 1, -1 is button 2
if(out==0){
   killwizard;
}

#Draw number of pages based on how many entries
#if back is pressed, load previous page via value of i
for(i=1;i<=length(metadata.material);0){
    draw_entry_page(i,metadata,dupdata);
    out = runwizard; #0 = cancel, 1 is button 1, -1 is button 2
    
    j=0;
    #declare here to avoid error when cleaning it at the end of the script
    #if the variable did not get instantiated
    if(out==0){
    killwizard;
   }
   else if((out==1)){
         if(any(i==dupdata(:,2))){
             for(j=1;j<=length(dupe);j=j+1){
                 if(i==dupe(j)){
	               layer_def_list{i} = struct;
	               layer_def_list{i}.layer = layer_def_list{ori(j)}.layer;
	               layer_def_list{i}.z = layer_def_list{ori(j)}.z;
	               layer_def_list{i}.material = layer_def_list{ori(j)}.material; 
                     }                 
                 }
             }         
         else{
	  layer_def_list{i} = struct;
	  layer_def_list{i}.layer = num2str(wizardgetdata(2))+":"+num2str(wizardgetdata(3));
	  layer_def_list{i}.z = (metadata.zmin{i}+metadata.zmax{i})/2;
         if (metadata.material{i} == "<Object defined dielectric>"){	  
	      layer_def_list{i}.material = metadata.index{i};
         }
         else{
             layer_def_list{i}.material = metadata.material{i};
         }
         }
	  i=i+1;       
   }

   else if((out==-1)){
	  if(i!=0){
		 i=i-1;
		 }
   }
}

#final confirmation page
newwizardpage("Export to GDS");
wizardwidget("label","Export Configuration (Leave blank for defaults)");
wizardwidget("string","GDS Filename\t(\"output.gds\"):","output.gds");
wizardwidget("string","Top Cell\t(\"model\"):",",model");
wizardwidget("label","==Layers Export Information==");
wizardwidget("label","Name\tLayer\tZpos");
for (i=1;i<=length(layer_def_list);i=i+1){
   
   wizardwidget("label",metadata.name{i}+"\t"+layer_def_list{i}.layer+"\t"+num2str(layer_def_list{i}.z)); 
}

out = runwizard;
if(out==0){
   killwizard;
}
else if(out==1){
	if (wizardgetdata(1)!=""){gds_filename_temp = "output/"+wizardgetdata(1);}
	else{gds_filename_temp = "output/TEMP_output";}
	if (wizardgetdata(2)!=""){top_cell = wizardgetdata(2);}
	else{top_cell = "model";}

	killwizard;

	#For scenarios where 2 objects have same height, we disable and enable as we export.       
	for (a=1;a<=length(metadata.name);a=a+1){
		select(metadata.name{a});
		set("enabled",false);           
	}	

	for (a=1;a<=length(metadata.name);a=a+1){ #cannot use i, it is used by the encrypted function and cleared
		layer_def = cell(1);
               layer_def{1} = layer_def_list{a};		
		gds_filename = gds_filename_temp+num2str(a)+".gds";	
                             
		select(metadata.name{a});
		set("enabled",true);		
		
                f_name = gdsopen(gds_filename);
                level_list = get_level_list("::model::");
                verbose = false;
                try{
                stck = extract_gds(level_list, layer_def, 0, f_name, verbose);
                }
                gdsclose(f_name);
		
		select(metadata.name{a});		
		set("enabled",false);	
	}
}
#reset back to original
for (a=1;a<=length(metadata.name);a=a+1){
	select(metadata.name{a});
	set("enabled",true);           
}

##reopen original file and delete temp file
#load(orifilename);
#del(copyfilename);
	
remove_copy_file(orifilename,copyfilename);

try{
clearfunctions;
clear(gds_filename);
clear(gds_filename_temp);
clear(out);
clear(top_cell);
clear(layer_def_list);
clear(a);
clear(metadata);
clear(dupdata);
clear(dupe);
clear(ori);
clear(j);
clear(f_name);
clear(i);
clear(layer_def);
clear(level_list);
clear(stck);
clear(thispath);
clear(verbose);
clear(orifilename);
clear(copyfilename);
}

cd(filedirectory(currentscriptname));
python("PY_klayout.py"); #Converts and merges the output file to be usable with up to date Klayout versions